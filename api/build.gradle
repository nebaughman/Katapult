plugins {
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.7.10'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

group 'net.nyhm'
version '0.8.1'
mainClassName = 'net.nyhm.katapult.example.MainKt'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'

    implementation 'com.github.ajalt.clikt:clikt:3.5.0'
    implementation 'io.javalin:javalin:4.6.4'
    implementation 'org.slf4j:slf4j-simple:2.0.1'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.13.4'
    implementation 'commons-codec:commons-codec:1.15'
    implementation 'com.github.spullara.mustache.java:compiler:0.9.10'
    implementation 'org.mindrot:jbcrypt:0.4'

    implementation 'org.jetbrains.exposed:exposed-core:0.39.2'
    implementation 'org.jetbrains.exposed:exposed-dao:0.39.2'
    implementation 'org.jetbrains.exposed:exposed-jdbc:0.39.2'
    implementation 'org.jetbrains.exposed:exposed-jodatime:0.39.2'

    implementation 'org.xerial:sqlite-jdbc:3.39.3.0'
    implementation 'org.postgresql:postgresql:42.5.0'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'

    implementation 'net.nyhm:pick:0.0.1' // see settings.gradle (github repo)
}

// https://stackoverflow.com/a/33026919/6004010
task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/project.properties").withWriter { w ->
            Properties p = new Properties()
            p['project'] = project.name
            p['version'] = project.version.toString()
            p.store w, "Generated by build process"
        }
    }
}

classes {
    dependsOn createProperties
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
}

test {
    useJUnitPlatform()
}


// An attempt to separate example into another sourceSet
/*
sourceSets {
    example {
        kotlin.srcDir 'src/example/kotlin'
    }
}
compileExampleKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
*/