plugins {
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.4.10'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

group 'net.nyhm'
version '0.7.2'

sourceCompatibility = 11
mainClassName = 'net.nyhm.katapult.example.MainKt'

repositories {
    jcenter()
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib'
    implementation 'org.jetbrains.kotlin:kotlin-reflect:1.4.10'
    implementation 'com.github.ajalt.clikt:clikt:3.0.1'
    implementation 'io.javalin:javalin:3.12.0'
    implementation 'org.slf4j:slf4j-simple:1.7.30'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.11.3'
    implementation 'commons-codec:commons-codec:1.15'
    implementation 'com.github.spullara.mustache.java:compiler:0.9.6'
    implementation 'org.mindrot:jbcrypt:0.4'
    implementation 'com.google.inject:guice:4.2.3'

    implementation 'org.jetbrains.exposed:exposed-core:0.28.1'
    implementation 'org.jetbrains.exposed:exposed-dao:0.28.1'
    implementation 'org.jetbrains.exposed:exposed-jdbc:0.28.1'
    implementation 'org.jetbrains.exposed:exposed-jodatime:0.28.1'

    implementation 'org.xerial:sqlite-jdbc:3.32.3.2'
    implementation 'org.postgresql:postgresql:42.2.18'

    implementation 'net.nyhm:pick:0.0.0' // see settings.gradle (github repo)

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

// https://stackoverflow.com/a/33026919/6004010
task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/project.properties").withWriter { w ->
            Properties p = new Properties()
            p['project'] = project.name
            p['version'] = project.version.toString()
            p.store w, "Generated by build process"
        }
    }
}

classes {
    dependsOn createProperties
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
}

// An attempt to separate example into another sourceSet
/*
sourceSets {
    example {
        kotlin.srcDir 'src/example/kotlin'
    }
}
compileExampleKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
*/